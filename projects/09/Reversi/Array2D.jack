/** Helper class for representing the board content */

class Array2D {
    field int width, height;
    field Array arr;

    /** Construct a 2D array with the given size */
    constructor Array2D new(int Awidth, int Aheight) {
        var int i, j;
        let width = Awidth;
        let height = Aheight;
        let arr = Array.new(height);

        let i = 0;
        while (i < height) {
            let arr[i] = Array.new(width);
            let i = i + 1;
        }

        // Initialize with empty (-1)
        let i = 0;
        while (i < height) {
            let j = 0;
            while (j < width) {
                do set(i, j, -1);
                let j = j + 1;
            }
            let i = i + 1;
        }

        return this;
    }

    /** Dispose this 2D array */
    method void dispose() {
        var int i;
        var Array row;

        // Iterate through each row and dispose it
        let i = 0;
        while (i < height) {
            let row = arr[i]; 
            do row.dispose();
            let i = i + 1;
        }

        // Dispose the array of rows
        do arr.dispose();
        return;
    }

    /** Set value at row i and column j */
    method void set(int i, int j, int value) {
        // Assume valid input
        var Array row;
        let row = arr[i];
        let row[j] = value;
        return;
    }

    /** Get value at row i and column j */
    method int get(int i, int j) {
        // Assume valid input
        var Array row;
        let row = arr[i];
        return row[j];
    }
}
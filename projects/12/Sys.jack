// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    // Memory init must be called before Math, Screen and Output init
    // Because those classes use Memory.alloc (Array.new) in their init functions
    function void init() {
        do Memory.init();   
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        do Main.main();
        return;
    }

    /** Halts the program execution. */
    // Use an infinite loop
    function void halt() {
        while (true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i, j;

        if (duration < 0) {
            do Sys.error(1);    // duration must be positive
        }

        let i = 0;
        while (i < duration) {
            let j = 0;
            while (j < 60) {
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    // There are 18 error codes (1 - 20 excluding 10, 11)
    function void error(int errorCode) {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}

//  1  Sys.wait              Duration must be positive
//  2  Array.new             Array size must be positive
//  3  Math.divide           Division by zero
//  4  Math.sqrt             Cannot compute square root of a negative number
//  5  Memory.alloc          Allocated memory size must be positive
//  6  Memory.alloc          Heap overflow
//  7  Screen.drawPixel      Illegal pixel coordinates
//  8  Screen.drawLine       Illegal line coordinates
//  9  Screen.drawRectangle  Illegal rectangle coordinates
// 12  Screen.drawCircle     Illegal center coordinates
// 13  Screen.drawCircle     Illegal radius
// 14  String.new            Maximum length must be non-negative
// 15  String.charAt         String index out of bounds
// 16  String.setCharAt      String index out of bounds
// 17  String.appendChar     String is full
// 18  String.eraseLastChar  String is empty
// 19  String.setInt         Insufficient string capacity
// 20  Output.moveCursor     Illegal cursor location